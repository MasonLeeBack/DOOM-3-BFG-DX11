/*
===========================================================================

Doom 3 BFG Edition GPL Source Code
Copyright (C) 1993-2012 id Software LLC, a ZeniMax Media company. 

This file is part of the Doom 3 BFG Edition GPL Source Code ("Doom 3 BFG Edition Source Code").  

Doom 3 BFG Edition Source Code is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

Doom 3 BFG Edition Source Code is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with Doom 3 BFG Edition Source Code.  If not, see <http://www.gnu.org/licenses/>.

In addition, the Doom 3 BFG Edition Source Code is also subject to certain additional terms. You should have received a copy of these additional terms immediately following the terms and conditions of the GNU General Public License which accompanied the Doom 3 BFG Edition Source Code.  If not, please request a copy in writing from id Software at the address below.

If you have questions concerning this license or the applicable additional terms, you may contact in writing id Software LLC, c/o ZeniMax Media Inc., Suite 120, Rockville, Maryland 20850 USA.

===========================================================================
*/

#include "global.inc"

SamplerState sampBump         : register(s0);
SamplerState sampDiffuse      : register(s1);
SamplerState sampSpec         : register(s2);
SamplerState sampLightFalloff : register(s3);
SamplerState sampLightProj    : register(s4);

Texture2D texBump             : register(t0); // per-surface bump map 
Texture2D texDiffuse          : register(t1); // per-surface diffuse map 
Texture2D texSpec             : register(t2); // per-surface specular map 
Texture2D texLightFalloff     : register(t3); // light falloff texture 
Texture2D texLightProj        : register(t4); // light projection texture 

struct PS_IN {
	half4 position	: SV_Position;
	half4 texcoord0	: TEXCOORD0;
	half4 texcoord1	: TEXCOORD1;
	half4 texcoord2	: TEXCOORD2;
	half4 texcoord3	: TEXCOORD3;
	half4 texcoord4	: TEXCOORD4;
	half4 texcoord5	: TEXCOORD5;
	half4 texcoord6	: TEXCOORD6;
	half4 color		: COLOR0;
};

struct PS_OUT {
	half4 color : SV_Target;
};

void main( PS_IN fragment, out PS_OUT result ) {
	half4 lightFalloff =	idtex2Dproj( texLightFalloff, sampLightFalloff, fragment.texcoord2 );
	half4 lightProj	=		idtex2Dproj( texLightProj   , sampLightProj   , fragment.texcoord3 );

	half4 bumpMap =			texBump     .Sample( sampBump   , fragment.texcoord1.xy );
	half4 YCoCG =			texDiffuse  .Sample( sampDiffuse, fragment.texcoord4.xy );
	half4 specMap =			texSpec     .Sample( sampSpec   , fragment.texcoord5.xy );

	half3 lightVector = normalize( fragment.texcoord0.xyz );
	half3 diffuseMap = ConvertYCoCgToRGB( YCoCG );

	half3 localNormal;
	localNormal.xy = bumpMap.wy - 0.5;
	localNormal.z = sqrt( abs( dot( localNormal.xy, localNormal.xy ) - 0.25 ) );
	localNormal = normalize( localNormal );

	const half specularPower = 10.0f;
	half hDotN = dot3( normalize( fragment.texcoord6.xyz ), localNormal );
	half3 specularContribution = _half3( pow( hDotN, specularPower ) );

	half3 diffuseColor = diffuseMap * rpDiffuseModifier.xyz;
	half3 specularColor = specMap.xyz * specularContribution * rpSpecularModifier.xyz;
	half3 lightColor = dot3( lightVector, localNormal ) * lightProj.xyz * lightFalloff.xyz;

	result.color.xyz = ( diffuseColor + specularColor ) * lightColor * fragment.color.xyz;
	result.color.w = 1.0;
}
